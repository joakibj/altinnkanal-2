buildscript {
    ext.kotlin_version = '1.2.61'

    ext.jetty_version = "9.4.11.v20180605"
    ext.cxf_version = '3.2.6'
    ext.caffeine_version = '2.6.2'
    ext.jackson_version = '2.9.6'
    ext.prometheus_version = '0.5.0'
    ext.konfig_version = '1.6.10.0'

    ext.kafka_version = '1.1.1'
    ext.confluent_version = '4.1.1'

    ext.kotlinlogging_version = '1.5.9'
    ext.logstash_encoder_version = '5.2'
    ext.logback_version = '1.2.3'
    ext.slf4j_version = '1.7.25'

    // test dependencies
    ext.kafka_embedded_version = '1.0.0'
    ext.kluent_version = '1.40'
    ext.spek_version = '1.2.0'
    ext.unboundid_version = '4.0.7'
    ext.mockito_kotlin_version = '2.0.0-RC1'
}

plugins {
    id 'no.nils.wsdl2java' version '0.10'
    id 'org.jmailen.kotlinter' version '1.16.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'application'
}

mainClassName = 'no.nav.altinnkanal.JettyBootstrapKt'
applicationDefaultJvmArgs = [
    '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
    '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}',
    '-XX:+UnlockExperimentalVMOptions',
    '-XX:+UseCGroupMemoryLimitForHeap',
    '-XX:+UseG1GC',
    '-XX:MaxRAMFraction=2',
    '-XshowSettings:vm',
    '-Dlogback.configurationFile=logback-remote.xml'
]

wsdl2java {
    wsdlDir = file("src/main/resources")
    wsdlsToGenerate = [
        ["$projectDir/src/main/resources/OnlineBatchReceiver.wsdl"]
    ]
}

compileKotlin.dependsOn wsdl2java

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
        windowsScript.text = windowsScript.text.replace('${NAV_TRUSTSTORE_PATH}', '%NAV_TRUSTSTORE_PATH%')
        windowsScript.text = windowsScript.text.replace('${NAV_TRUSTSTORE_PASSWORD}', '%NAV_TRUSTSTORE_PASSWORD%')
    }
}

test {
    testLogging.showStandardStreams = true
    useJUnitPlatform {
        includeEngines 'spek'
    }
}

repositories {
    maven {
        name 'Confluent maven repo'
        url 'http://packages.confluent.io/maven/'
    }
    jcenter()
}

dependencies {
    implementation project(':altinnkanal-schemas')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.eclipse.jetty:jetty-server:$jetty_version"
    implementation "org.eclipse.jetty:jetty-servlet:$jetty_version"

    implementation ("io.confluent:kafka-avro-serializer:$confluent_version") {
        exclude group: 'org.slf4j'
        exclude group: 'log4j'
        exclude group: 'org.apache.kafka'
    }
    implementation "org.apache.kafka:kafka-clients:$kafka_version"

    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
    implementation ("org.apache.cxf:cxf-rt-ws-security:$cxf_version") {
        exclude group: 'org.opensaml'
    }

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"
    implementation "io.prometheus:simpleclient_servlet:$prometheus_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "com.natpryce:konfig:$konfig_version"

    implementation "io.github.microutils:kotlin-logging:$kotlinlogging_version"
    runtimeOnly "ch.qos.logback:logback-classic:$logback_version"
    runtimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
    testImplementation ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation ("org.jetbrains.spek:spek-data-driven-extension:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "no.nav:kafka-embedded-env:$kafka_embedded_version"
    testImplementation "com.unboundid:unboundid-ldapsdk:$unboundid_version"
    testImplementation 'org.junit.platform:junit-platform-runner:1.2.0'
}
