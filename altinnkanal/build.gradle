buildscript {
    ext.jetty_version = "9.4.10.v20180503"
    ext.kafka_test_version = '0.6.0'
    ext.kotlin_version = '1.2.41'
    ext.confluent_version = '4.0.0'
    ext.cxf_version = '3.2.4'
    ext.kafka_version = '1.0.0'
    ext.jackson_version = '2.9.5'
    ext.prometheus_version = '0.4.0'
    ext.logstash_encoder_version = '5.1'
    ext.mockito_kotlin_version = '2.0.0-alpha04'
    ext.kluent_version = '1.38'
    ext.spek_version = '1.1.5'
    ext.caffeine_version = '2.6.2'
    ext.unboundid_version = '4.0.6'
    ext.kotlinlogging_version = '1.4.9'
    ext.logback_version = '1.2.3'
    ext.slf4j_version = '1.7.25'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'no.nils.wsdl2java' version '0.10'
    id 'org.jmailen.kotlinter' version '1.12.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

mainClassName = 'no.nav.altinnkanal.JettyBootstrapKt'
applicationDefaultJvmArgs = [
        '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
        '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}',
        '-Xms200m',
        '-XX:+UnlockExperimentalVMOptions',
        '-XX:+UseCGroupMemoryLimitForHeap',
        '-XX:+UseG1GC',
        '-XX:MaxRAMFraction=2',
        '-XshowSettings:vm',
        '-Dlogback.configurationFile=logback-remote.xml'
]

wsdl2java {
    wsdlDir = file("src/main/resources")
    wsdlsToGenerate = [
        ['-xjc', '-b', "$projectDir/src/main/resources/bindings.xjb", "$projectDir/src/main/resources/OnlineBatchReceiver.wsdl"]
    ]
}

compileKotlin.dependsOn wsdl2java

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
        windowsScript.text = windowsScript.text.replace('${NAV_TRUSTSTORE_PATH}', '%NAV_TRUSTSTORE_PATH%')
        windowsScript.text = windowsScript.text.replace('${NAV_TRUSTSTORE_PASSWORD}', '%NAV_TRUSTSTORE_PASSWORD%')
    }
}

test {
    testLogging.showStandardStreams = true
}

repositories {
    maven {
        name 'Confluent maven repo'
        url 'http://packages.confluent.io/maven/'
    }

    maven {
        name 'iApp Nexus 3 server - releases'
        url 'https://repo.adeo.no/repository/maven-releases/'
    }
    jcenter()
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.opensaml'
    all { resolutionStrategy { force 'org.apache.commons:commons-compress:1.16.1' } }
    all { resolutionStrategy { force 'org.mockito:mockito-core:2.18.3' } }
}

dependencies {
    implementation project(':altinnkanal-schemas')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.eclipse.jetty:jetty-server:$jetty_version"
    implementation "org.eclipse.jetty:jetty-servlet:$jetty_version"
    implementation "io.confluent:kafka-avro-serializer:$confluent_version"
    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxrs:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"
    implementation "io.prometheus:simpleclient_servlet:$prometheus_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "io.github.microutils:kotlin-logging:$kotlinlogging_version"
    runtimeOnly "ch.qos.logback:logback-classic:$logback_version"
    runtimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
    testImplementation ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation ("org.jetbrains.spek:spek-data-driven-extension:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "no.nav.common:embedded.kafka:$kafka_test_version"
    testImplementation "com.unboundid:unboundid-ldapsdk:$unboundid_version"
}
