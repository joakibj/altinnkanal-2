buildscript {
    ext.jetty_version = "9.4.9.v20180320"
    ext.kafka_test_version = '0.6.0'
    ext.kotlin_version = '1.2.31'
    ext.confluent_version = '4.0.0'
    ext.cxf_version = '3.2.4'
    ext.kafka_version = '1.0.0'
    ext.jackson_version = '2.9.5'
    ext.prometheus_version = '0.3.0'
    ext.logstash_encoder_version = '5.0'
    ext.mockito_kotlin_version = '1.5.0'
    ext.kluent_version = '1.35'
    ext.spek_version = '1.1.5'
    ext.caffeine_version = '2.6.2'
    ext.unboundid_version = '4.0.5'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name "JCenter Gradle Plugins"
            url "https://dl.bintray.com/gradle/gradle-plugins"
        }
    }

    dependencies {
        classpath 'no.nils:wsdl2java:0.10'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

apply plugin: 'no.nils.wsdl2java'
apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

mainClassName = 'no.nav.altinnkanal.JettyBootstrapKt'
applicationDefaultJvmArgs = [
        '-Xms128M',
        '-Xmx512M',
        '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
        '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}'
]


wsdl2java {
    wsdlDir = file("src/main/resources")
    wsdlsToGenerate = [
            ["$projectDir/src/main/resources/OnlineBatchReceiver.wsdl"]
    ]
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

compileKotlin.dependsOn wsdl2java

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
        windowsScript.text = windowsScript.text.replace('${NAV_TRUSTSTORE_PATH}', '%NAV_TRUSTSTORE_PATH%')
        windowsScript.text = windowsScript.text.replace('${NAV_TRUSTSTORE_PASSWORD}', '%NAV_TRUSTSTORE_PASSWORD%')
    }
}

test {
    testLogging.showStandardStreams = true
}

repositories {
    maven {
        name 'Confluent maven repo'
        url 'http://packages.confluent.io/maven/'
    }

    maven {
        name 'iApp Nexus 3 server - releases'
        url 'https://repo.adeo.no/repository/maven-releases/'
    }
    jcenter()
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all { resolutionStrategy { force 'org.apache.commons:commons-compress:1.16.1' } }
}

dependencies {
    implementation project(':altinnkanal-schemas')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.eclipse.jetty:jetty-server:$jetty_version"
    implementation "org.eclipse.jetty:jetty-servlet:$jetty_version"

    implementation "io.confluent:kafka-avro-serializer:$confluent_version"
    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxrs:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"
    implementation "io.prometheus:simpleclient_servlet:$prometheus_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"

    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "org.slf4j:log4j-over-slf4j:1.7.25"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    testCompile "org.jetbrains.spek:spek-api:$spek_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testRuntimeOnly "org.apache.kafka:kafka_2.12:$kafka_version"
    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "no.nav.common:embedded.kafka:$kafka_test_version"
    testImplementation "com.unboundid:unboundid-ldapsdk:$unboundid_version"
}
